project(
  'dde-cooperation',
  'cpp',
  version: '0.0.1',
  default_options: [
    'cpp_std=c++17',
    'warning_level=3',
  ],
)

cpp_compiler = meson.get_compiler('cpp')
if cpp_compiler.get_argument_syntax() == 'clang'
  compiler_args = cpp_compiler.get_supported_arguments([
  ])
else
  compiler_args = cpp_compiler.get_supported_arguments([
    '-Wno-c++20-extensions',
  ])
endif
compiler_args += cpp_compiler.get_supported_arguments([
  '-Wno-vla',
])
add_project_arguments(compiler_args, language: 'cpp')

prefixdir = get_option('prefix')
datadir = prefixdir / get_option('datadir')
libexecdir = prefixdir / get_option('libexecdir')

cxx = meson.get_compiler('cpp')
stdcxxfs = cxx.find_library('stdc++fs', required: false)

thread = dependency('threads')
uuid = dependency('uuid', required: true)
fmt = dependency('fmt', required: true)
spdlog = dependency('spdlog', required: true)
openssl = dependency('openssl', required: true)
uv = dependency('libuv', required: true)
tl_expected = dependency('tl-expected', method: 'cmake', modules: ['tl::expected'], required: true)
glibmm = dependency('glibmm-2.4', required: true)
giomm = dependency('giomm-2.4', required: true)
libevdev = dependency('libevdev', required: true)
fuse3 = dependency('fuse3', required: true)
xcb = dependency('xcb', required: true)
xcb_randr = dependency('xcb-randr', required: true)
xcb_xinput = dependency('xcb-xinput', required: true)
xcb_xfixes = dependency('xcb-xfixes', required: true)

qt5 = import('qt5')
qt5dep = dependency('qt5', modules: ['Core', 'Gui', 'DBus', 'Widgets'])
dtk_widget = dependency('dtkwidget', required: true)

protobuf = dependency('protobuf', required: true)
protoc_exec = find_program('protoc')
protoc = generator(protoc_exec,
  output: ['@BASENAME@.pb.cc', '@BASENAME@.pb.h'],
  arguments: ['--proto_path=@CURRENT_SOURCE_DIR@', '--cpp_out=@BUILD_DIR@', '--experimental_allow_proto3_optional', '@INPUT@']
)

systemd = dependency('systemd')
systemd_user_unit_dir = systemd.get_pkgconfig_variable('systemduserunitdir')

executable_install_dir = join_paths(libexecdir, 'dde-cooperation')

subdir('src')

install_data('misc/dbus/services/com.deepin.Cooperation.service',
             install_dir: join_paths(datadir, 'dbus-1', 'services'))

data = configuration_data()
data.set('executable_install_dir', executable_install_dir )

configure_file(
  input: 'misc/systemd-service/dde-cooperation.service.in',
  output: '@BASENAME@',
  configuration: data,
  install_dir: systemd_user_unit_dir,
)

install_data('misc/config/compressor-singlefile.conf',
             install_dir: '/usr/share/applications/context-menus')

install_data('misc/sendfile/sendFiles.sh',
             install_dir: executable_install_dir)
